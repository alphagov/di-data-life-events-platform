plugins {
    id "java"
    id "org.sonarqube" version "4.3.1.3277"
    id "com.diffplug.spotless" version "6.21.0"
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

sonar {
    properties {
        property "sonar.projectKey", "alphagov_di-data-life-events-platform"
        property "sonar.organization", "alphagov"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

spotless {
    java {
        target 'lib/src/*/java/**/*.java', 'lambdas/**/src/*/java/**/*.java'

        importOrder('com.acme', '', 'java|javax', '\\#com.acme', '\\#')
        removeUnusedImports()
    }
}

allprojects { project ->
    dependencyLocking {
        lockAllConfigurations()
    }
    task resolveAndLockAll {
        doFirst {
            assert gradle.startParameter.writeDependencyLocks
        }
        doLast {
            configurations.findAll {
                it.canBeResolved
            }.each {it.resolve()}
        }
    }
}
